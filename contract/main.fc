const slice skip_address = "Ef8Nb7157K5bVxNKAvIWreRcF0RcUlzcCA7lwmewWVNtqM3s"a;
const int min_balance = 1000000000;

int slices_equal? (slice a, slice b) asm "SDEQ";

() recv_internal (int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    var cs = in_msg_full.begin_parse();
    var flags = cs~load_uint(4);
    if (flags & 1) {
        return ();
    }
    
    slice sender = cs~load_msg_addr();
    int op = in_msg_body~load_uint(32);

    if (op == op::generate()) {
        send_raw_message(
            begin_cell()
                .store_uint(0x18, 6)
                .store_slice(skip_address)
                .store_coins(my_balance - min_balance)
                .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
                .store_uint(op::generate_response(), 32)
                .store_slice(sender)
                .store_ref(begin_cell().store_slice(in_msg_body).end_cell())
            .end_cell(),
            2
        );
    }
    elseif (op == op::generate_response()) {
        throw_unless(701, slices_equal?(sender, skip_address));
        randomize_lt();
        send_raw_message(
            begin_cell()
                .store_uint(0x18, 6)
                .store_slice(in_msg_body~load_msg_addr())
                .store_coins(my_balance - min_balance)
                .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
                .store_uint(op::generate_response(), 32)
                .store_uint(random(), 256)
                .store_ref(in_msg_body~load_ref())
            .end_cell(),
            2
        );
    }
}